--Entire Program
Prog . Program ::= [Def] ;
entrypoints Program ;
comment "//" ;
comment "/*" "*/" ;

--Definition
DType . Def ::= "type" Ident "=" Type ;
DTerm . Def ::= "expr" Ident ":" Type "=" Expr ;
(:[]) . [Def] ::= Def ;
(:)   . [Def] ::= Def [Def] ;

--Type
TyVar    . Type4 ::= Ident ;
TyUnit   . Type4 ::= "I" ;
TySum    . Type2 ::= Type2 "+" Type3 ;
TyTensor . Type3 ::= Type3 "*" Type4 ;
TyFunc   . Type1 ::= Type1 "->" Type2 ;
TyRec    . Type4 ::= "fix" Ident "." Type4 ;
coercions Type 4 ;

--Term
TmVar    . Term4 ::= Ident ;
TmUnit   . Term4 ::= "unit" ;
TmLeft   . Term4 ::= "inl" Term4 ;
TmRight  . Term4 ::= "inr" Term4 ;
TmTensor . Term3 ::= Term3 "," Term4 ;
TmArrow  . Term2 ::= Term2 "=>" Term3 ;
TmFold   . Term4 ::= "fold" Type Term4 ;
TmLin    . Term1 ::= Term1 "|" Term2 ;
TmLabel  . Term4 ::= Ident Term4 ;
coercions Term 4 ;

--Expr
ExTerm  . Expr2 ::= Term ;
ExApp   . Expr2 ::= Expr2 Term ;
ExComp  . Expr1 ::= Expr1 ";" Expr2 ;
ExFlip  . Expr2 ::= "~" Expr2 ;
ExTrace . Expr2 ::= Term "where" Ident ":" Type ;
coercions Expr 2 ;