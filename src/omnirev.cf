--Entire Program
Prog . Program ::= [Def] ;
entrypoints Program ;
comment "//" ;
comment "/*" "*/" ;

--Definition
DType . Def ::= "type" Ident "=" Type ;
DFunc . Def ::= "func" Ident ":" Type "<->" Type "=" Func ;
DExpr . Def ::= "expr" Ident ":" Type "=" Expr ;
(:[]) . [Def] ::= Def ;
(:)   . [Def] ::= Def [Def] ;

--Expression
EUnit   . Expr3 ::= "()" ;
ETensor . Expr2 ::= Expr2 "*" Expr3 ;
ESum    . Expr1 ::= Expr1 "+" Expr2 ;
ELeft   . Expr3 ::= "left" Expr3 ;
ERight  . Expr3 ::= "right" Expr3 ;
EStar   . Expr3 ::= "¬" Expr3 ;
EVar    . Expr3 ::= Ident ;
ERec    . Expr3 ::= "<" Expr3 ">" ;
EApp    . Expr3 ::= Func Expr3 ;
EProj   . Expr3 ::= "measure" Expr3 ;
coercions Expr 3 ;

--Type
TUnit   . Type3 ::= "unit" ;
TTensor . Type2 ::= Type2 "*" Type3 ;
TSum    . Type1 ::= Type1 "+" Type2 ;
TStar   . Type3 ::= "¬" Type3 ;
TVar    . Type3 ::= Ident ;
TRec    . Type3 ::= "µ" Ident "." Type ;
coercions Type 3 ;

--Function
FId        . Func4 ::= "id" ;
FComp      . Func1 ::= Func1 ";" Func2 ;
FTensor    . Func3 ::= Func3 "*" Func4 ;
FTensUnit  . Func4 ::= "unit*" ;
FTensAssoc . Func4 ::= "assoc*" ;
FTensSym   . Func4 ::= "sym*" ;
FSum       . Func2 ::= Func2 "+" Func3 ;
FSumAssoc  . Func4 ::= "assoc+" ;
FSumSym    . Func4 ::= "sym+" ;
FDistrib   . Func4 ::= "distrib" ;     
FEval      . Func4 ::= "eval" Type ;
FDagger    . Func4 ::= "^" Func4 ;
FVar       . Func4 ::= Ident ;
coercions Func 4 ;